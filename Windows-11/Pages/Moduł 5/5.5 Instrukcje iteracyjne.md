## 5.5: Instrukcje iteracyjne
## Wprowadzenie

Instrukcje iteracyjne w skryptach **BATCH** pozwalają na wielokrotne wykonywanie zestawu poleceń, dopóki spełniony jest określony warunek lub dla każdego elementu w zbiorze danych. Dzięki nim możesz np. przetwarzać wszystkie pliki w katalogu, wykonywać polecenie dla różnych parametrów lub tworzyć pętle z licznikiem. To kluczowa rzecz do realizacji powtarzalnych fragmentów kodu.

### Definicje

<div data-hint="info">

**Pętla**: Mechanizm, który pozwala na powtarzanie bloku kodu.  
W skryptach **BATCH** najczęściej używa się polecenia `FOR` do tworzenia pętli.

</div>

## Podstawowa składnia

| Polecenie     | Opis                                                                                          |
|:--------------|:----------------------------------------------------------------------------------------------|
| `FOR`         | Wykonuje polecenie dla każdego elementu w zbiorze (np. pliki, ciągi znaków).                  |
| `FOR /L`      | Tworzy pętlę z licznikiem (np. od $$1$$ do $$10$$).                                                      |
| `FOR /F`      | Przetwarza dane tekstowe (np. linie w pliku lub wynik polecenia).                              |
| `EXIT /B`     | Kończy wykonywanie skryptu lub funkcji z określonym kodem wyjścia.                              |


## Pętla `FOR` - podstawy

Polecenie `FOR` pozwala iterować po zbiorze elementów, takich jak pliki w katalogu lub lista wartości.  
Zmienna pętli (np. `%%i`, `%%f`) przechowuje aktualny element.

**Składnia**:

```dos
FOR %%zmienna IN (zbiór) DO (polecenie)
```

- `%%zmienna`: Nazwa zmiennej pętli. Najlepiej wykorzystać pojedynczą literę, np. `%%i`, `%%j`, `%%f`.
- `(zbiór)`: Zbiór elementów, np. lista plików `*.txt` lub lista wartości (`Ala`, `Adam`, `Ewa`).
- `(polecenie)`: Polecenie wykonywane dla każdego elementu.

#### Przykład z zbiorem danych

```dos
@echo off
CHCP 65001>nul

FOR %%i IN (Ala, Adam, Ewa) DO (
    echo Witaj %%i!
)

pause
```
![Witanie użytkowników z zbioru danych poprzez pętlę.](https://raw.githubusercontent.com/Edu-Koala-V/images/main/Windows_11/Images/batch-for-imiona.webp)


#### Przykład wyświetlanie wszystkich plików `.txt` w katalogu

```dos
@echo off
CHCP 65001>nul

echo Lista plików tekstowych w bieżącym katalogu:
echo ===========================================
FOR %%f IN (*.txt) DO (
    echo Plik: %%f
)

pause
```

- `%%f IN (*.txt)`: Iteruje po wszystkich plikach z rozszerzeniem `.txt` w bieżącym katalogu.
- `echo Plik: %%f`: Wyświetla nazwę każdego pliku.

![Wynik skryptu wyświetlającego pliki tekstowe.](https://raw.githubusercontent.com/Edu-Koala-V/images/main/Windows_11/Images/batch-for-pliki.webp)

<div data-hint="warning">

Jeśli w katalogu nie ma plików `.txt`, pętla nie wykona się ani razu. Upewnij się, że zbiór elementów nie jest pusty!

</div>


### Pętla `FOR /L` - pętla z licznikiem

Polecenie `FOR /L` służy do tworzenia pętli z licznikiem (*iteratorem*) przejść w pętli (*iteracji*).

**Składnia**:

```dos
FOR /L %%zmienna IN (start, krok, koniec) DO (kod)
```

  - `start`: Wartość początkowa iteratora.
  - `krok`: Wartość, o którą zwiększa się lub zmniejsza iterator w każdej iteracji (może być ujemna).
  - `koniec`: Wartość końcowa iteratora (włącznie).

#### Przykład: Liczenie od 1 do 5

```dos
@echo off
CHCP 65001>nul

echo Liczenie od 1 do 5:
echo ===========================================
FOR /L %%i IN (1, 1, 5) DO (
    echo Iteracja: %%i
)

pause
```

- `(1, 1, 5)`: Licznik `%%i` zaczyna od $$1$$, zwiększa się o $$1$$, kończy na $$5$$.

![Wynik skryptu wyświetlającego odliczenie od 1 do 5.](https://raw.githubusercontent.com/Edu-Koala-V/images/main/Windows_11/Images/batch-for-l-1-5.webp)


#### Przykład: Odliczanie od 10 do 1

```dos
@echo off
CHCP 65001>nul

echo Odliczanie od 10 do 1:
echo ===========================================
FOR /L %%i IN (10, -1, 1) DO (
    echo Odliczanie: %%i
)

pause
```

- `(10, -1, 1)`: Licznik `%%i` zaczyna od $$10$$, zmniejsza się o $$1$$, kończy na $$1$$.

![Wynik skryptu wyświetlającego odliczenie od 10 do 1.](https://raw.githubusercontent.com/Edu-Koala-V/images/main/Windows_11/Images/batch-for-l-10-1.webp)


### Pętla `FOR /F` - przetwarzanie tekstu

Polecenie `FOR /F` służy do przetwarzania wyników poleceń, zawartości plików tekstowych lub ciągów znaków, dzieląc je na tokeny (np. słowa w linii).

**Składnia**:

```dos
FOR /F "opcje" %%zmienna IN ("ciąg" | 'polecenie' | plik) DO (kod)
```


- `"opcje"`: Parametry, np. `"delims="` (separator) i `"tokens="` (numer tokenu). Przetwarzany tekst jest dzielony na tokeny, czyli słowa. Domyślnie są one rozdzielane przy wykryciu spacji lub tabulacji i dla każdego tokenu w linii nadaje się *indeks*. Jeżeli w linii mamy tekst: `ala ma kota`, to separuje się te słowa jako tokeny, wykrywając spację. Otrzymujemy wtedy taki wynik:
    1.  `ala`
    2.  `ma`
    3.  `kota`
  - W `"delims="` możesz określić inny separator lub pozostawić pustym, by nie stosować separatorów w liniach.
  - W `"tokens="` możesz określić *indeksy* tokenów, które mają być iterowane, np. `"tokens=2,5"`. Pamiętaj, że jeżeli nie ma tokenu na danym *indeksie*, zostanie zwrócona pusta wartość.
- `"ciąg"`: Stały tekst do przetworzenia.
- `'polecenie'`: Polecenie **CMD**, którego wynik jest przetwarzany.
- `plik`: Plik tekstowy, którego linie są przetwarzane.


#### Przykład: Przetwarzanie wyniku polecenia `dir`

```dos
@echo off
CHCP 65001>nul

echo Nazwy katalogów w bieżącym folderze:
echo ===========================================
FOR /F "delims=" %%d IN ('dir /ad /b') DO (
    echo Katalog: %%d
)

pause
```

- `'dir /ad /b'`: Polecenie zwraca listę katalogów w formacie prostym.
  - `/ad`: wyświetli tylko katalogi.
  - `/b`: wyświetli tylko nazwy plików i katalogów.
- `"delims="`: Wyłącza domyślne separatory (spacje, tabulatory), dzięki czemu cała linia jest przetwarzana jako jeden element.

**Wynik**: Wyświetla nazwy wszystkich katalogów w bieżącym folderze.

![Wynik skryptu wyświetlającego katalogi w bieżącym katalogu.](https://raw.githubusercontent.com/Edu-Koala-V/images/main/Windows_11/Images/batch-for-f-dirs.webp)

<div data-hint="success">

Używaj `"delims="`, jeśli chcesz przetwarzać całe linie bez dzielenia na słowa. Domyślnie `FOR /F` dzieli linie na tokeny, co może obciąć nazwy zawierające spacje.

</div>


#### Przykład: Czytanie pliku tekstowego

Załóżmy, że masz plik `lista.txt` z następującą zawartością:

<small>lista.txt</small>

```
Janek
Kasia
Marek
```

Skrypt czytający ten plik:

```dos
@echo off
CHCP 65001>nul

echo Lista imion z pliku lista.txt:
echo ===========================================
FOR /F "delims=" %%n IN (lista.txt) DO (
    echo Imię: %%n
)

pause
```
**Wynik**:


![Wynik skryptu wyświetlającego zawartość pliku .txt.](https://raw.githubusercontent.com/Edu-Koala-V/images/main/Windows_11/Images/batch-for-f-read-file.webp)


<div data-hint="warning">

Plik tekstowy musi być zapisany w kodowaniu **UTF-8**, jeśli zawiera polskie znaki lub emoji, a skrypt musi używać `CHCP 65001`.

</div>


## Zagnieżdżone pętle

Możesz używać pętli wewnątrz innych pętli, aby przetwarzać bardziej złożone dane. Każda zagnieżdżona pętla musi używać innej zmiennej (np. `%%i`, `%%j`).

Poniższy skrypt wypisuje kolejno wartości `%%i` i `%%j` w zagnieżdżonej pętli.

```dos
@echo off

echo.
echo ================================================
echo.

FOR /L %%i IN (1, 1, 3) DO (
    echo i = %%i
    FOR /L %%j IN (1, 1, 3) DO (
        echo     j = %%j
    )
)

echo.
echo ================================================
echo.

FOR /L %%i IN (1, 1, 3) DO (
    FOR /L %%j IN (1, 1, 3) DO (
        echo i = %%i     j = %%j
    )
)

echo.
echo ================================================
echo.

pause
```


## Stosowanie zmiennych w pętlach

<div data-hint="info">

**WAŻNE!**
Aby móc używać zmiennych w pętlach i przypisywać im nowe wartości, musisz na początku skryptu dodać <span style="white-space: nowrap;">`setlocal EnableDelayedExpansion`</span>. Ta instrukcja włącza tzw. opóźnioną ekspansję zmiennych. Od teraz, chcąc używać zmiennych, należy pisać `!zmienna!` zamiast `%zmienna%`. Dodatkowo, deklaracja zmiennej poprzez `SET` będzie wymagała dopisania `/A`, jeśli wykonujesz operacje arytmetyczne.

</div>

Poniżej przedstawiam przykład, który wykorzystuje zmienne iteracyjne, poddaje je działaniu iloczynu i wyświetla ich wyniki.

```dos
@echo off
CHCP 65001>nul
setlocal EnableDelayedExpansion

echo     Mnożenie par współrzędnych (i,j):
echo +=========================================+
echo.

FOR /L %%i IN (1, 1, 3) DO (
    echo ===========================================
    echo Wyniki mnożenia przez %%i
    echo ===========================================
    echo.
    echo [i, j]
    echo.
    FOR /L %%j IN (1, 1, 3) DO (
        set /a wynik=%%i*%%j
        echo [%%i, %%j] = !wynik!
    )
)

pause
```

-----

## Ćwiczenie: Tabliczka mnożenia

Wykonaj skrypt **BATCH**, który z pomocą pętli zagnieżdżonej zwróci poniższy wynik:

![Tabliczka mnożenia w CMD - skrypt BATCH](https://raw.githubusercontent.com/Edu-Koala-V/images/main/Windows_11/Images/batch-for-if-tabliczka-mnożenia.webp)

<details>
<summary>Rozwiązanie</summary>

Aby stworzyć tabliczkę mnożenia, kluczowe jest odpowiednie formatowanie wyniku, aby liczby były wyrównane.

Używamy zmiennej `wiersz` do budowania linii wyniku, dodając spacje w zależności od długości liczby
($$1-9$$: dwie spacje, $$10-99$$: jedna spacja, $$100-\infty$$: bez spacji).

Aby poprawnie sformatować tabliczkę, musieliśmy zdecydować, kiedy stosować znaki `[`, `][` i `]` w wierszach: 
- `[` otwiera kolumnę
- `][` oddziela sąsiednie kolumny
-  `]` zamyka wiersz

Zmienna `!wynik!` przechowuje wyniki mnożenia, a statyczne elementy, takie jak nagłówki i separatory, budują "interfejs" skryptu.

```dos
@echo off
color 03
CHCP 65001 >nul
setlocal EnableDelayedExpansion

REM Definicja nagłówka i separatorów
set kolumny=        [   1 ][   2 ][   3 ][   4 ][   5 ][   6 ][   7 ][   8 ][   9 ][  10 ][  11 ][  12 ][  13 ]
set separator=+=================================================================================================+
set separator2=###################################################################################################

REM Wyświetlenie nagłówka
echo %separator%
echo                                      TABLICZKA MNOŻENIA
echo %separator%
echo %kolumny%
echo %separator2%

REM Generowanie tabliczki mnożenia
FOR /L %%i IN (1, 1, 13) DO (
    REM Formatowanie wiersza
    if %%i GEQ 10 (
        set "wiersz=%%i #"
    ) else (
        set "wiersz=%%i  #"
    )
    FOR /L %%j IN (1, 1, 13) DO (
        set /a wynik=%%i*%%j
        REM Wyrównanie liczby do 3 znaków
        set "formatowany=  !wynik!"
        if !wynik! GEQ 10 set "formatowany= !wynik!"
        if !wynik! GEQ 100 set "formatowany=!wynik!"
        if %%j EQU 1 (
            set "wiersz=!wiersz!    [ !formatowany!"
        ) else (
            set "wiersz=!wiersz! ][ !formatowany!"
        )
    )
    echo !wiersz! ]
)

REM Wyświetlenie stopki
echo %separator2%
echo %separator%
echo.
pause
```

</details>

-----

## Przerwanie pętli

Nie ma w **BATCH** bezpośredniego odpowiednika `break`, ale możesz użyć `GOTO` lub `EXIT /B`, aby przerwać pętlę lub skrypt.

<div data-hint="warning">

Samo `EXIT` zakończy działanie skryptu lub konsoli polecenia **CMD**. Dlatego należy dopisać opcję `/B`, która powoduje wyjście z danego bloku kodu.

</div>

#### Przykład: Przerwanie pętli po znalezieniu pliku

```dos
@echo off
CHCP 65001>nul

echo Szukanie pliku test.txt:
echo ===========================================
FOR %%f IN (*.txt) DO (
    IF "%%f"=="test.txt" (
        echo Znaleziono test.txt!
        GOTO :koniec
    )
    echo Plik: %%f
)
:koniec

pause
```

- `GOTO :koniec`: Przeskakuje do etykiety `:koniec`, przerywając pętlę po znalezieniu `test.txt`.


---


# Podcast AI

<audio src="https://raw.githubusercontent.com/Edu-Koala-V/audio_podcast/refs/heads/main/Windows_11/Audio/Batch Scripting_ Iteration and Loops with FOR.wav" controls>
Twoja przeglądarka nie wspiera odtwarzania plików audio.
</audio>